(ns bones.http.handlers-test
  (:require [bones.http.handlers :as handlers]
            [bones.http.auth :as auth :refer [token]]
            [clojure.test :refer [deftest testing is]]
            [clojure.edn :as edn]
            [io.pedestal.test :refer [response-for]]
            [io.pedestal.http :as bootstrap]
            [io.pedestal.http.route.definition :refer [defroutes]]
            [bones.http.service :as service]
            [ring.mock.request :as mock]
            [ring.middleware.session.store :as store]
            [ring.util.codec :as codec]
            [schema.core :as s]
            ))

(defn hello [args req]
  (let [who (get-in args [:who])]
    {:greeting (str "hello " who)}))

(handlers/register-command :hello {:who s/Str})

(defn thrower [args req]
  (throw (ex-info "something fake" {:other "stuff" :status 555})))

(handlers/register-command :thrower {:what s/Keyword})

(defn edn-body [{:keys [body]}]
  (-> body edn/read-string))

(def routes (-> (handlers/map->CQRS {}) .start :routes))

(def service
  (::bootstrap/service-fn (bootstrap/create-servlet (service/service routes {}))))

(defn edn-post
  ([body-params]
   (edn-post body-params {}))
  ([body-params headers]
   (edn-post body-params headers "/api/command"))
  ([body-params headers path]
   (response-for service
                 :post path
                 :body (pr-str body-params)
                 :headers (merge {"Content-Type" "application/edn"
                                  "Accept" "application/edn"}
                                 headers))))

(defn login-post [body-params]
  (edn-post body-params {} "/api/login"))

(defn edn-get
  ([path]
   (edn-get path {}))
  ([path headers]
   (response-for service
                 :get path
                 :headers (merge {"Content-Type" "application/edn"
                                  "Accept" "application/edn"}
                                 headers))))

(def valid-cookie
  ;; session must have identity in it for buddy
  ;; can't generate a valid cookie for some reason
  ;; need to use one generated by ring's session-response
  (let [
        ;; I thought it would be something like this
        ;; session (auth/session {:identity {:user-d 123}})
        session "vRu8o3avetgkDgiLqyCJ6T75m6V8fjt6VnRP3klThvz7ackbHcXLoaMX6izXeT4F--JtGidgQhNx3CmWYeNxWgl1t2mGWjW56pg8KVnAfZ2F8="
        ;; cookie (codec/url-encode session)
        cookie session
        ]
    {"Cookie" (str "bones-session=" cookie ";")}))

(def valid-token
  ;; any data will do here I think?
  {"authorization" (str "Token " (token {:identity {:user-d 123}}))})

(def invalid-token
  {"authorization" "Token nuthin"})

(deftest register-command-test
  (testing "optional handler argument"
    (is (handlers/register-command :test {} ::hello)))
  (testing "explicit handler with a namespace"
    (is (handlers/register-command :test {} ::handlers/echo)))
  (testing "non existing function throws error"
    (is (thrown? clojure.lang.ExceptionInfo
                 (handlers/register-command :nope {})))))

(deftest command-resource-test
  (testing "invalid token"
    (let [body-params {:command :hello :args {:who "mr teapot"}}
          response (edn-post body-params invalid-token)]
      (is (= "{:message \"Not Authenticated\"}" (:body response)))
      (is (= "application/edn"  (get (:headers response)  "Content-Type")))
      (is (= 403 (:status response)))))

  (testing "non-existant args"
    (let [response (edn-post {:command :echo #_no-args} valid-token)]
      (is (= "{:message \"args not valid\", :data {:args missing-required-key}}" (:body response)))
      (is (= (get (:headers response) "Content-Type") "application/edn"))
      (is (= (:status response) 401))))

  (testing "non-existant command"
    (let [response (edn-post {:command :nuthin} valid-token)]
      (is (= {:message "command not found"
              :data {:message "command not found: :nuthin"
                     :available-commands '(:echo :hello :thrower :login)}}
             (edn-body response)))
      (is (= (get (:headers response) "Content-Type") "application/edn"))
      (is (= (:status response) 401))))

  (testing "args that are something other than a map"
    (let [response (edn-post {:command :echo :args [:not :a-map]} valid-token)]
      (is (= "{:message \"args not valid\", :data {:args (not (map? [:not :a-map]))}}" (:body response)))
      (is (= (get (:headers response) "Content-Type") "application/edn"))
      (is (= (:status response) 401))))

  (testing "built-in echo command with valid args"
    (let [response (edn-post {:command :echo :args {:yes :allowed}} valid-token)]
      (is (= (:body response) "{:yes :allowed}"))
      (is (= (get (:headers response) "Content-Type") "application/edn"))
      (is (= (:status response) 200))))

  (testing "registered command with valid args"
    (let [body-params {:command :hello :args {:who "mr teapot"}}
          response (edn-post body-params valid-token)]
      (is (= (:body response) "{:greeting \"hello mr teapot\"}"))
      (is (= (get (:headers response) "Content-Type") "application/edn"))
      (is (= (:status response) 200))))

  (testing "an exception's ex-data gets rendered and status gets used"
    (let [body-params {:command :thrower :args {:what :blowup}}
          response (edn-post body-params valid-token)]
      (is (= (get (:headers response) "Content-Type") "application/edn"))
      (is (= "{:message \"something fake\", :data {:other \"stuff\"}}" (:body response)))
      (is (= 555 (:status response))))))

(defn login [args req]
  (if (= "shortandstout" (:password args))
    {:user-id 123 :role "appliance"}))

(handlers/register-command :login {:username s/Str :password s/Str})

(def valid-login {:command :login :args {:username "mr teapot" :password "shortandstout"}})

(def invalid-login {:command :login :args {:username "mr teapot" :password "tallandfuzzy"}})

(deftest session-test
  (testing "Set-Cookie is set on response if login command returns data(non-nil)"
    (let [response (login-post valid-login)]
      (is (= 200 (:status response)))
      (is (= (get (:headers response) "Content-Type") "application/edn"))
      (is (contains? (:headers response) "Set-Cookie"))
      (is (contains? (edn-body response) :token))))
  (testing "no session is set if login command returns nil"
    (let [response (login-post invalid-login)]
      (is (= 422 (:status response)))
      (is (not (contains? (:headers response) "Set-Cookie")))
      (is (= {:message "Invalid Credentials"} (edn-body response)))
      (is (= (get (:headers response) "Content-Type") "application/edn")))))

(deftest command-list-resource-test
  ;; this is order/time sensitive because of the registered commands
  (testing "get"
    (let [response (edn-get "/api/command")]
      (is (= '{:available-commands ({:args {Any Any}
                                    :command (enum :echo)}
                                   {:args {:who java.lang.String}
                                    :command (enum :hello)}
                                   {:args {:what Keyword}
                                    :command (enum :thrower)}
                                    ;; login should probably be filtered out?
                                   {:args {:username java.lang.String
                                           :password java.lang.String}
                                    :command (enum :login)})}
             (edn-body response)))
      (is (= (get (:headers response) "Content-Type") "application/edn"))
      (is (= (:status response) 200)))))

(defn graph [params req]
  (let [g (-> params :q edn/read-string)]
    {:graph g}))

(handlers/register-query-handler :graph {:q s/Str} )

(deftest query-resource-test
  (testing "a valid token or session is required"
    (let [response (edn-get "/api/query?q=abc" invalid-token)]
      (is (= 403 (:status response)))))
  (testing "the query handler can be overwritten by `register-query-handler'"
    (let [response (edn-get "/api/query?q=abc" valid-token)]
      (is (= "{:graph abc}" (:body response)))
      (is (= 200 (:status response))))))
